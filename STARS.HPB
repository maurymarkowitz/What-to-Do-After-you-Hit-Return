100 REM *** STARS *** STARS *** STARS *** STARS *** STARS ***
110 REM *** COPYRIGHT 1974 BY PCC, P.O. BOX 310,MENLO PARK CA
120 PRINT "WELCOME TO MY GALAXY. I'M IN CHARGE OF THE STARS HERE."
130 PRINT "PLAY MY GAME *STARS* AND GET SOME STARS FOR YOURSELF!"
140 PRINT
150 PRINT "WHAT TO KNOW HOW TO PLAY";
160 INPUT Z$[1,1]
170 IF Z$ <> "Y" THEN 280
180 REM *** HERE IS HOW TO PLAY
190 PRINT
200 PRINT "I WILL THINK OF A WHOLE NUMBER FROM 1 TO 100."
210 PRINT "TRY TO GUESS MY NUMBER. AFTER YOU GUESS, I"
220 PRINT "WILL TYPE ONE OF MORE STARS (*). THE CLOSER"
230 PRINT "YOU ARE TO MY NUMBER, THE MORE STARS WILL I TYPE."
240 PRINT "ONE STAR (*) MEANS YOU ARE FAR AWAY FROM MY"
250 PRINT "NUMBER. SEVEN STARS (*******) MEANS YOU ARE VERY,"
260 PRINT "VERY, VERY CLOSE TO MY NUMBER!!!"
270 REM *** COMPUTER THINKS" OF A NUMBER FROM 1 TO 100
280 LET X=INT(100*RND(0))+1
290 PRINT
300 PRINT "OK, STARSEEKER, I AM THINKING OF A NUMBER. START GUESSING."
310 REN *** GUESSING BEGINS
320 LET N=1
330 PRINT
340 PRINT "WHAT IS YOUR GUESS";
350 INPUT G
360 IF G=X THEN 550
370 LET D=ABS(G-X)
380 IF D >= 64 THEN 500
390 IF D >= 32 THEN 490
400 IF D >= 16 THEN 480
410 ID D >= 8 THEN 470
420 IF D >= 4 THEN 460
430 IF D >=2 THEN 450
440 PRINT " *";
450 PRINT " *";
460 PRINT " *";
470 PRINT " *";
480 PRINT " *";
490 PRINT " *";
500 PRINT " *";
510 PRINT
520 LET N=N+1
530 GOTO 330
540 REM *** PLAYER HAS GUESSED THE GALACTIC NUMBER
550 FOR K=1 TO 18
560 PRINT " **";
570 NEXT K
580 PRINT "!!!'
590 PRINT "THAT'S IT!!! YOU GUESSEDMY COSMIC NUMBER IN";N;"GUESSES."
600 PRINT
610 PRINT "WANT TO PLAY AGAIN";
620 INPUT Z$[1,1]
630 IF Z$="Y" THEN 280
640 PRINT "OK, GOODBYE FOR NOW. PLAY WITH ME AGAIN SOMEWHEN."
650 END
