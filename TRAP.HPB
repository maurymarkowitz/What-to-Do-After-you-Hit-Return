100 REMARK *** TRAP *** TRAP *** TRAP *** TRAP *** TRAP *** TRAP
110 REMARK *** COPYRIGHT 1974 BY PEOPLE'S COMPUTER COMPANY
120 PRINT "DO YOU WANT INSTRUCTIONS";
130 INPUT Z$[1,1]
140 IF Z$<>"Y" THEN 290
150 PRINT
160 PRINT "I WILL THINK OF A NUMBER BETWEEN 1 TO 100."
170 PRINT "TRY TO GUESS MY NUMBER. ENTER TWO NUMBERS, TRYING"
180 PRINT "TO TRAP MY NUMBER B YOUR TWO TRAP NUMBERS. I'LL"
190 PRINT "TELL YOU IF YOU HAVE TRAPPED BY NUMBER OF IF MY"
200 PRINT "NUMBER IS SMALLER THAN YOUR TWO TRAP NUMBERS OR"
210 PRINT "IF MY NUMBER IS LARGER THAN YOUR TWO TRAP NUMBERS."
220 PRINT "IF I TELL YOU THAT YOU HAVE TRAPPED MY NUMBER, I"
230 PRINT "MEAN THAT MY NUMBER IS *BETWEEN* YOUR TRAP NUMBERS"
240 PRINT "OR - PERHAPS MY NUMBER IS THE SAME AS ONE OF YOUR"
250 PRINT "TRAP NUMBERS."
260 PRINT
270 PRINT "!!!IMPORTANT!!! IF YOU THINK YOU KNOW MY NUMBER, THEN"
280 PRINT "ENTER YOUR GUESS FOR *BOTH* TRAP NUMBERS."
290 PRINT
300 LET =INT(100*RND(0))+1
310 PRINT "I'M THINKING...THINKING...AH! I HAVE A NUMBER!"
320 LET K=1
330 PRINT
340 PRINT "FIRST TRAP NUMBER";
350 INPUT A
360 PRINT "SECOND TRAP NUMBER";
370 INPUT B
380 LET T=SGN(X-A)+SGN(X-B)
390 GOTO T+3 OF 430,410,400,410,450
400 IF A=B THEN 480
410 PRINT "MY NUMBER IS TRAPPED BY YOUR NUMBERS."
420 GOTO 460
430 PRINT "MY NUMBER IS SMALLER THAN YOUR TRAP NUMBERS."
440 GOTO 460
450 PRINT "MY NUMBER IS LARGER THAN YOUR TRAP NUMBERS."
460 LET K=K+1
470 GOTO 330
480 PRINT "YOU GOT IT IN";K;"GUESSES...LET'S PLAY AGAIN, LUCKY."
490 GOTO 290
500 END